
declare_args() {
    is_debug = false
}

if (target_os == "") {
    target_os = "wasm"
}

# All binary targets will get this list of configs by default.
_shared_binary_target_configs = [ "//build:compiler_defaults" ]

# Apply that default list to the binary target types.
set_defaults("executable") {
    configs = _shared_binary_target_configs

    # Executables get this additional configuration.
    configs += [ "//build:executable_ldconfig" ]
}
set_defaults("static_library") {
    configs = _shared_binary_target_configs
}
set_defaults("shared_library") {
    configs = _shared_binary_target_configs
}
set_defaults("source_set") {
    configs = _shared_binary_target_configs
}

if (target_os == "wasm") {
    set_default_toolchain("//build/toolchain:emcc")
} else if (target_os == "linux") {
    set_default_toolchain("//build/toolchain:clang")
} else if (target_os == "win") {
    set_default_toolchain("//build/toolchain:msvc")
} else {
    assert(0, "unsupported target_os")
}
